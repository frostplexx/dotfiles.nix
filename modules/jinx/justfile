#!/usr/bin/env just --justfile

alias d := deploy
alias u := upgrade
alias e := edit

# macOS need nh darwin switch and NixOS needs nh os switch
nix_cmd := `if [ "$(uname)" = "Darwin" ]; then echo "darwin"; else echo "os"; fi`
# Use GITHUB_TOKEN from 1Password to prevent rate limiting
NIX_CONFIG := "access-tokens = github.com=$(op read op://Personal/GitHub/token)"
# Dotfiles storage location
DOTFILES_FOLDER := "$HOME/dotfiles.nix"

# Set default recipe
[private]
default:
    @printf "\nWelcome to \033[0;36mjinx\033[0m - The nix configuration manager.\n"
    @printf "Run \033[0;34mjinx --choose\033[0m to get an interactiv list of commands"
    @printf " or \033[0;34mjinx help\033[0m for the help page.\n"
    @just --list --list-prefix "  " --list-heading $'Available Commands:\n\n'

import "./deploy/deploy.just"
import "./update/update.just"
import "./maintain/maintain.just"
import "./rollback/rollback.just"
import "./misc/misc.just"
import "./edit-nvim/edit-nvim.just"

[private]
lint:
    @nix fmt . 2> /dev/null
    @nix run nixpkgs#statix -- check {{DOTFILES_FOLDER}}
    @nix run nixpkgs#deadnix -- -eq {{DOTFILES_FOLDER}}

